plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlin.android'
//    alias(libs.plugins.kotlin.android)
}
def publishVersion=rootProject.ext.publishVersion
// Notice: 2022/1/11 Android依赖发布函数库

tasks.register('javadoc', Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

tasks.register('sourceJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}
tasks.register('zipRepo', Zip) {
    from "$buildDir/repo"
    archiveFileName = 'io.zip'
    destinationDirectory = file(buildDir)
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'io.github.hearthappy'
                artifactId = 'vma-ktx'
                version = publishVersion
                artifacts {
                    archives(javadocJar)
                    archives(sourceJar)
                }
                pom {
                    name = 'vma-ktx'
                    description = 'ViewModelAutomation extension library'
                    url = 'https://github.com/HeartHappy/ViewModelAutomationX'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'HeartHappy'
                            name = 'এ᭄白ຼ龙້໌ᮨꦿ'
                            email = '1096885636@qq.com'
                        }
                    }

                    scm {
                        connection = 'scm:git://github.com/HeartHappy/ViewModelAutomationX'
                        developerConnection = 'scm:git:ssh://github.com/HeartHappy/ViewModelAutomationX'
                        url = 'https://github.com/HeartHappy/ViewModelAutomationX'
                    }
                }
            }
        }
        repositories {
            maven {
                url = uri("${buildDir}/repo") // 本地仓库路径
            }
        }
    }
    signing {
        //gpg签名认证
        sign(publishing.publications.release)
    }
    // 设置 zipRepo 任务依赖于发布任务
    tasks.named('zipRepo') { dependsOn(publishReleasePublicationToMavenRepository) }

    // 确保发布任务完成后总是执行 zipRepo 任务，即使发布任务失败
    tasks.named('publishReleasePublicationToMavenRepository') {
        finalizedBy(tasks.named('zipRepo'))
    }
}

android {
    namespace 'com.hearthappy.vma_ktx'
    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        minSdk rootProject.ext.minSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    kotlinOptions {
        jvmTarget = rootProject.ext.kotlinJvmTarget
    }
}

dependencies {
//    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
//    implementation libs.androidx.activity
    implementation libs.kotlinx.corountines

    implementation libs.kotlin.reflect
//    implementation libs.lifecycle.runtime.ktx
    implementation libs.lifecycle.viewmodel.ktx


}